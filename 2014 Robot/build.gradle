apply plugin: 'java'

ext {
	sdkRoot = new File(System.getProperty('user.home'), "sunspotfrcsdk")
	robotClass = 'team1277.org.robot.MainRobot'
	
	team = 1277
	remoteAddress = "10." + new StringBuilder(team as String).insert(2, '.') + ".2"
}

configurations {
	boot
	romizer
	antTasks
}

repositories {
	mavenCentral()
}

dependencies {
	compile files("$sdkRoot/lib/wpilibj.jar")
	compile files("$sdkRoot/lib/networktables-crio.jar")
	
	boot files("$sdkRoot/lib/squawk.jar")
	
	romizer files("$sdkRoot/bin/romizer_classes.jar")
	romizer files("$sdkRoot/bin/squawk.jar")
	romizer files("$sdkRoot/bin/squawk_device_classes.jar")
	romizer files("$sdkRoot/bin/translator_classes.jar")
	
	antTasks("org.apache.ant:ant-commons-net:1.9.3") {
		module("commons-net:commons-net:1.4.1") {
			dependencies "oro:oro:2.0.8:jar"
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
}

// Load Ant tasks
ant.taskdef(resource: 'com/sun/spot/sdk/tool/ftp/antlib.xml') {
	classpath{
		pathelement(location: "$sdkRoot/ant/sdktoolftp.jar")
		pathelement(location: "$sdkRoot/ant/commons-net-1.4.1.jar")
		pathelement(location: "$sdkRoot/ant/jakarta-oro-2.0.8.jar")
	}
}

ant.taskdef(name: 'ftp',
	classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
	classpath: configurations.antTasks.asPath
)

apply from: 'gradle/compile.gradle'
apply from: 'gradle/preverify.gradle'
apply from: 'gradle/jar.gradle'
apply from: 'gradle/suite.gradle'
apply from: 'gradle/configure.gradle'
apply from: 'gradle/deploy.gradle'
apply from: 'gradle/remote.gradle'
apply from: 'gradle/eclipse.gradle'
apply from: 'gradle/emulator.gradle'

task wrapper(type: Wrapper) {
	gradleVersion = '1.10'
}